/-
Copyright (c) 2026 Samuel Schlesinger. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Samuel Schlesinger
-/

module

public import Cslib.Languages.Probabilistic.Transcript
public import Cslib.Probability.Product

/-!
# Lazy sampling of random functions

`RandomOracle.answer` samples a response on the first occurrence of a query and reuses it on
later calls. It supports arbitrary query domains, dependent response types, and a separate
distribution for each query. A cache may also contain answers fixed in advance.

On finite domains and finite response types, `runState_answer_complete` proves the stronger
form of eager/lazy equivalence: completing the cache after an adaptive program has the same
joint law as completing it before the program. `eval_eq_lazy` discards the final table, and
`uniform_eval_eq_lazy` specializes to a uniformly random function.

`memoize` supplies an ordinary association-list implementation. `runState_memoize` relates its
complete executions to the mathematical cache, including on infinite query domains. Efficiency
is supplied separately by `Cslib.Computability.Probabilistic.Memoize`.
`memoize_cache_bounds` derives cache cardinality and entry provenance from the program's query
bounds; the computational layer also accounts for the encoded sizes of keys and replies.

## References

O. Goldreich, S. Goldwasser, S. Micali,
[*How to Construct Random Functions*](https://www.wisdom.weizmann.ac.il/~oded/X/ggm-jacm.pdf),
Section 3.3, uses consistent lazy sampling in the adaptive hybrid argument.
-/

@[expose] public section

namespace Cslib.RandomOracle

open Probability

universe u

variable {Query : Type u} {Response : Query → Type u} {α : Type u}

/-- Previously assigned responses, indexed by their typed queries. -/
abbrev Cache (Response : Query → Type u) := (q : Query) → Option (Response q)

/-- Return a cached answer, or sample and store one fresh answer. -/
noncomputable def answer [DecidableEq Query] (distribution : (q : Query) → PMF (Response q))
    (q : Query) : StateT (Cache Response) PMF (Response q) := fun cache =>
  match cache q with
  | some value => PMF.pure (value, cache)
  | none => (distribution q).map (fun value => (value, Function.update cache q (some value)))

/-- A cache hit does not change the cache or draw fresh randomness. -/
theorem answer_of_some [DecidableEq Query] (distribution : (q : Query) → PMF (Response q))
    (cache : Cache Response) (q : Query) (value : Response q) (h : cache q = some value) :
    answer distribution q cache = PMF.pure (value, cache) := by
  simp [answer, h]

/-- A previously unseen query draws exactly one response from its designated distribution. -/
theorem answer_of_none [DecidableEq Query] (distribution : (q : Query) → PMF (Response q))
    (cache : Cache Response) (q : Query) (h : cache q = none) :
    answer distribution q cache =
      (distribution q).map (fun value => (value, Function.update cache q (some value))) := by
  simp [answer, h]

section List

variable {Value : Type u} [BEq Query]

/-- A memoized sampler preserves any property of its key-value entries and returns a value with
that property at the requested key. Cache keys need not be unique. -/
theorem memoize_invariant [LawfulBEq Query] (distribution : Query → PMF Value)
    (property : Query → Value → Prop) (key : Query) (cache : List (Query × Value))
    (hcache : ∀ entry ∈ cache, property entry.1 entry.2)
    (hsample : ∀ value ∈ (distribution key).support, property key value)
    (result : Value × List (Query × Value))
    (hresult : result ∈ (memoize distribution key cache).support) :
    property key result.1 ∧ ∀ entry ∈ result.2, property entry.1 entry.2 := by
  cases hlookup : cache.lookup key with
  | none =>
    rw [memoize_of_none _ _ _ hlookup] at hresult
    change result ∈ ((distribution key).map (fun value => (value, (key, value) :: cache))).support
      at hresult
    obtain ⟨value, hvalue, rfl⟩ := (PMF.mem_support_map_iff _ _ _).mp hresult
    exact ⟨hsample value hvalue, by simpa using And.intro (hsample value hvalue) hcache⟩
  | some value =>
    rw [memoize_of_some _ _ _ _ hlookup] at hresult
    change result ∈ (PMF.pure (value, cache)).support at hresult
    simp only [PMF.mem_support_pure_iff] at hresult
    subst result
    obtain ⟨before, after, hlist, _⟩ := List.lookup_eq_some_iff.mp hlookup
    exact ⟨hcache (key, value) (by simp [hlist]), hcache⟩

/-- Each memoized call adds at most one entry. Every new entry records an actual query and an
answer in its sampling distribution's support. Repeated calls still appear in the transcript. -/
theorem runState_memoize_transcript (distribution : Query → PMF Value)
    (program : OracleComp Query (fun _ => Value) α) (cache cache' : List (Query × Value))
    (result : α) (transcript : OracleComp.Transcript (fun _ : Query => Value))
    (h : (result, cache', transcript) ∈ (OracleComp.runState
      (OracleComp.withTranscript (memoize distribution)) program (cache, [])).support) :
    cache'.length ≤ cache.length + transcript.length ∧
      ∀ entry ∈ cache', entry ∈ cache ∨
        (⟨entry.1, entry.2⟩ ∈ transcript ∧ entry.2 ∈ (distribution entry.1).support) := by
  refine OracleComp.runState_invariant (OracleComp.withTranscript (memoize distribution))
    (fun (current, seen) => current.length ≤ cache.length + seen.length ∧
      ∀ entry ∈ current, entry ∈ cache ∨
        (⟨entry.1, entry.2⟩ ∈ seen ∧ entry.2 ∈ (distribution entry.1).support))
    ?_ program (cache, []) (by simp) result (cache', transcript) h
  rintro q ⟨current, seen⟩ ⟨hcount, hentries⟩ answer next hnext
  rw [OracleComp.withTranscript, PMF.mem_support_map_iff] at hnext
  obtain ⟨⟨value, current'⟩, hvalue, heq⟩ := hnext
  cases heq
  have hold : ∀ entry ∈ current, entry ∈ cache ∨
      (⟨entry.1, entry.2⟩ ∈ ⟨q, answer⟩ :: seen ∧ entry.2 ∈ (distribution entry.1).support) := by
    intro entry hentry
    rcases hentries entry hentry with h | ⟨hseen, hsupport⟩
    · exact Or.inl h
    · exact Or.inr ⟨List.mem_cons_of_mem _ hseen, hsupport⟩
  cases hlookup : current.lookup q with
  | none =>
    rw [memoize_of_none _ _ _ hlookup] at hvalue
    change (answer, current') ∈ ((distribution q).map
      (fun value => (value, (q, value) :: current))).support at hvalue
    rw [PMF.mem_support_map_iff] at hvalue
    obtain ⟨fresh, hfresh, heq⟩ := hvalue
    cases heq
    refine ⟨by simp only [List.length_cons]; lia, ?_⟩
    intro entry hentry
    rcases List.mem_cons.mp hentry with rfl | hentry
    · exact Or.inr ⟨List.mem_cons_self, hfresh⟩
    · exact hold entry hentry
  | some cached =>
    rw [memoize_of_some _ _ _ _ hlookup] at hvalue
    change (answer, current') ∈ (PMF.pure (cached, current)).support at hvalue
    simp only [PMF.mem_support_pure_iff] at hvalue
    cases hvalue
    exact ⟨by simp only [List.length_cons]; lia, hold⟩

/-- Query bounds control the number and provenance of all new cache entries. The sampler's own
support bound can then bound stored replies, without assuming that each call grows by a constant. -/
theorem memoize_cache_bounds (distribution : Query → PMF Value)
    (program : OracleComp Query (fun _ => Value) α) (size : Query → ℕ) (calls maxSize : ℕ)
    (hqueries : program.HasQueryBounds size calls maxSize)
    (cache cache' : List (Query × Value)) (result : α)
    (h : (result, cache') ∈ (OracleComp.runState (memoize distribution) program cache).support) :
    cache'.length ≤ cache.length + calls ∧
      ∀ entry ∈ cache', entry ∈ cache ∨
        (size entry.1 ≤ maxSize ∧ entry.2 ∈ (distribution entry.1).support) := by
  rw [← OracleComp.runState_withTranscript (memoize distribution) program cache [],
    PMF.mem_support_map_iff] at h
  obtain ⟨⟨value, state⟩, hstate, heq⟩ := h
  cases heq
  obtain ⟨hcount, hentries⟩ := runState_memoize_transcript distribution program cache state.1
    result state.2 hstate
  obtain ⟨hcalls, hsize⟩ := hqueries _ (memoize distribution) cache state.1 result state.2 hstate
  refine ⟨hcount.trans (Nat.add_le_add_left hcalls _), ?_⟩
  intro entry hentry
  rcases hentries entry hentry with h | ⟨hseen, hsupport⟩
  · exact Or.inl h
  · exact Or.inr ⟨hsize _ hseen, hsupport⟩

/-- Interpret a finite association list as a partial function. The first occurrence of a key
determines its value, so arbitrary initial lists are supported without a uniqueness invariant. -/
def ofList (cache : List (Query × Value)) : Cache (fun _ : Query => Value) :=
  fun q => cache.lookup q

@[simp] theorem ofList_nil : ofList ([] : List (Query × Value)) = fun _ => none := rfl

variable [LawfulBEq Query] [DecidableEq Query]

/-- Prepending a cache entry updates exactly its key in the represented partial function. -/
theorem ofList_cons (cache : List (Query × Value)) (q : Query) (value : Value) :
    ofList ((q, value) :: cache) = Function.update (ofList cache) q (some value) := by
  funext query
  by_cases h : query = q
  · subst query
    simp [ofList]
  · simp [ofList, List.lookup_cons, h, beq_eq_false_iff_ne.mpr h]

/-- The ordinary list-based memoizer implements the mathematical random oracle, preserving the
result and represented final cache for every adaptive program, even on an infinite query domain. -/
theorem runState_memoize (distribution : Query → PMF Value)
    (program : OracleComp Query (fun _ => Value) α) (cache : List (Query × Value)) :
    (OracleComp.runState (memoize distribution) program cache).map
        (fun (result, cache') => (result, ofList cache')) =
      OracleComp.runState (answer distribution) program (ofList cache) := by
  apply OracleComp.runState_map_state
  intro q cache
  cases h : cache.lookup q with
  | none =>
    rw [memoize_of_none _ _ _ h, answer_of_none distribution (ofList cache) q h]
    change ((distribution q).map (fun value => (value, (q, value) :: cache))).map _ = _
    simp only [PMF.map_comp, Function.comp_def, ofList_cons]
  | some value =>
    rw [memoize_of_some _ _ _ _ h, answer_of_some distribution (ofList cache) q value h]
    exact PMF.pure_map _ _

/-- A memoized sampler is an ordinary stateful probabilistic program. Its interpretation agrees
with the mathematical cache, including the represented final state. -/
theorem eval_simulateState_memoize (sample : Query → ProbComp Value)
    (program : OracleComp Query (fun _ => Value) α) (cache : List (Query × Value)) :
    (ProbComp.eval (OracleComp.simulateState (memoize sample) program cache)).map
        (fun (result, cache') => (result, ofList cache')) =
      OracleComp.runState (answer (fun q => ProbComp.eval (sample q))) program (ofList cache) := by
  unfold ProbComp.eval
  rw [OracleComp.eval_simulateState]
  simpa only [OracleComp.eval_memoize] using
    runState_memoize (fun q => OracleComp.eval (fun q => q.elim) (sample q)) program cache

end List

section Finite

variable [Fintype Query] [∀ q, Finite (Response q)]

/-- Independently fill all missing entries while retaining the previously assigned answers. -/
noncomputable def complete (distribution : (q : Query) → PMF (Response q))
    (cache : Cache Response) : PMF ((q : Query) → Response q) :=
  PMF.pi (fun q => (cache q).elim (distribution q) PMF.pure)

/-- Completing the empty cache samples all coordinates independently. -/
@[simp] theorem complete_empty (distribution : (q : Query) → PMF (Response q)) :
    complete distribution (fun _ => none) = PMF.pi distribution := rfl

/-- A completion agrees with every answer already in the cache. -/
theorem complete_agrees (distribution : (q : Query) → PMF (Response q))
    (cache : Cache Response) (table : (q : Query) → Response q)
    (htable : table ∈ (complete distribution cache).support)
    (q : Query) (value : Response q) (h : cache q = some value) : table q = value := by
  have hq := (PMF.mem_support_pi_iff _ _).mp htable q
  simpa only [h, Option.elim_some, PMF.mem_support_pure_iff] using hq

/-- Fixing one cache entry fixes the corresponding coordinate of its completion. -/
theorem complete_update [DecidableEq Query] (distribution : (q : Query) → PMF (Response q))
    (cache : Cache Response) (q : Query) (value : Response q) :
    complete distribution (Function.update cache q (some value)) =
      PMF.pi (Function.update (fun q => (cache q).elim (distribution q) PMF.pure)
        q (PMF.pure value)) := by
  unfold complete
  congr 1
  funext query
  by_cases h : query = q
  · subst query
    simp
  · simp [Function.update_of_ne h]

/-- Answering one query and completing the cache gives the same joint answer and table as
completing first and reading the table. -/
theorem answer_complete [DecidableEq Query] (distribution : (q : Query) → PMF (Response q))
    (cache : Cache Response) (q : Query) :
    (answer distribution q cache).bind (fun (value, cache') =>
      (complete distribution cache').map (value, ·)) =
        (complete distribution cache).map (fun table => (table q, table)) := by
  cases h : cache q with
  | none =>
    simp only [answer, h, PMF.bind_map, Function.comp_def, complete_update]
    simpa only [h, Option.elim_none, complete] using
      PMF.pi_bind_update_pair (fun q => (cache q).elim (distribution q) PMF.pure) q
  | some value =>
    simp only [answer, h, PMF.pure_bind]
    symm
    exact PMF.map_congr_on_support _ _ _ (fun table htable => by
      rw [complete_agrees distribution cache table htable q value h])

/-- Eager/lazy equivalence including the final complete table, for every adaptive program and
every initial cache. Later continuations can inspect both the result and that table. -/
theorem runState_answer_complete [DecidableEq Query]
    (distribution : (q : Query) → PMF (Response q))
    (program : OracleComp Query Response α) (cache : Cache Response) :
    (OracleComp.runState (answer distribution) program cache).bind (fun (result, cache') =>
      (complete distribution cache').map (result, ·)) =
        (complete distribution cache).bind (fun table =>
          (OracleComp.eval (fun q => PMF.pure (table q)) program).map (·, table)) := by
  have h := OracleComp.runState_simulation (answer distribution)
    (fun q table => (PMF.pure (table q)).map (·, table)) (complete distribution)
    (fun q cache => by
      simpa only [PMF.pure_map, PMF.map, Function.comp_def, PMF.pure_bind] using
        answer_complete distribution cache q) program cache
  simpa only [OracleComp.runState_readOnly] using h

/-- Lazy evaluation from an empty cache agrees with sampling the entire independent function
in advance, even for adaptive, randomized programs with repeated queries. -/
theorem eval_eq_lazy [DecidableEq Query] (distribution : (q : Query) → PMF (Response q))
    (program : OracleComp Query Response α) :
    (PMF.pi distribution).bind (fun table =>
      OracleComp.eval (fun q => PMF.pure (table q)) program) =
        (OracleComp.runState (answer distribution) program (fun _ => none)).map Prod.fst := by
  have h := congrArg (PMF.map Prod.fst)
    (runState_answer_complete distribution program (fun _ => none))
  simpa only [complete_empty, PMF.map_bind, PMF.map_comp, PMF.map, Function.comp_def,
    PMF.bind_const, PMF.bind_pure, PMF.bind_bind, PMF.pure_bind] using h.symm

/-- A uniformly sampled function can be replaced by a consistent lazy uniform oracle. -/
theorem uniform_eval_eq_lazy [DecidableEq Query] [∀ q, Fintype (Response q)]
    [∀ q, Nonempty (Response q)] (program : OracleComp Query Response α) :
    (PMF.uniformOfFintype ((q : Query) → Response q)).bind (fun table =>
      OracleComp.eval (fun q => PMF.pure (table q)) program) =
        (OracleComp.runState (answer (fun q => PMF.uniformOfFintype (Response q)))
          program (fun _ => none)).map Prod.fst := by
  simpa only [PMF.pi_uniformOfFintype] using
    eval_eq_lazy (fun q => PMF.uniformOfFintype (Response q)) program

end Finite

section ListFinite

variable {Value : Type u} [Fintype Query] [Finite Value]
  [BEq Query] [LawfulBEq Query]

/-- An ordinary list-based cache implements eager independent function sampling. -/
theorem eval_eq_memoize (distribution : Query → PMF Value)
    (program : OracleComp Query (fun _ => Value) α) :
    (PMF.pi distribution).bind (fun table =>
      OracleComp.eval (fun q => PMF.pure (table q)) program) =
        (OracleComp.runState (memoize distribution) program []).map Prod.fst := by
  classical
  rw [eval_eq_lazy]
  have h := congrArg (PMF.map Prod.fst) (runState_memoize distribution program [])
  simpa only [PMF.map_comp, Function.comp_def, ofList_nil] using h.symm

/-- Implement a finite random function using arbitrary injective key representations and mapped
values. The client sees the represented values; it need not know the finite sampling type. -/
theorem eval_eq_memoize_map {Key' Value' : Type u} [BEq Key'] [LawfulBEq Key']
    (keyMap : Query ↪ Key') (valueMap : Value → Value')
    (distribution : Query → PMF Value) (sample' : Key' → PMF Value')
    (hsample : ∀ key, sample' (keyMap key) = (distribution key).map valueMap)
    (program : OracleComp Query (fun _ => Value') α) :
    (PMF.pi distribution).bind (fun table =>
      OracleComp.eval (fun key => PMF.pure (valueMap (table key))) program) =
      (OracleComp.runState (fun key => memoize sample' (keyMap key)) program []).map Prod.fst := by
  let represent := fun cache : List (Query × Value) =>
    cache.map (fun entry => (keyMap entry.1, valueMap entry.2))
  have hstep : ∀ key cache,
      ((memoize distribution key cache).map (fun (value, next) => (valueMap value, next))).map
        (fun (value, next) => (value, represent next)) =
      memoize sample' (keyMap key) (represent cache) := by
    intro key cache
    simpa only [PMF.map_comp, Function.comp_def, represent, PMF.monad_map_eq_map] using
      memoize_map keyMap valueMap distribution sample' hsample key cache
  have hrun := OracleComp.runState_map_state
    (fun key cache => (memoize distribution key cache).map
      (fun (value, next) => (valueMap value, next)))
    (fun key => memoize sample' (keyMap key)) represent hstep program []
  have houtput := congrArg (PMF.map Prod.fst) hrun
  simp only [PMF.map_comp, Function.comp_def, represent, List.map_nil] at houtput
  rw [← houtput]
  have hquery (key : Query) :
      OracleComp.runState (memoize distribution) (valueMap <$> OracleComp.query key) =
        fun cache => (memoize distribution key cache).map
          (fun (value, next) => (valueMap value, next)) := by
    funext cache
    simp only [OracleComp.runState_map, OracleComp.runState_query]
  simpa only [OracleComp.eval_simulate, OracleComp.eval_map, OracleComp.eval_query,
    PMF.pure_map, OracleComp.runState_simulate, hquery] using eval_eq_memoize distribution
      (OracleComp.simulate (fun key => valueMap <$> OracleComp.query key) program)

/-- A program using memoized closed samplers has the eager random-function semantics. -/
theorem eval_simulateState_memoize_eq_eager (sample : Query → ProbComp.{u, u} Value)
    (program : OracleComp Query (fun _ => Value) α) :
    ProbComp.eval (Prod.fst <$> OracleComp.simulateState.{u} (memoize sample) program []) =
      (PMF.pi (fun q => ProbComp.eval (sample q))).bind (fun table =>
        OracleComp.eval (fun q => PMF.pure (table q)) program) := by
  classical
  rw [eval_eq_lazy]
  have h := congrArg (PMF.map Prod.fst) (eval_simulateState_memoize.{u} sample program [])
  simpa only [ProbComp.eval_map, PMF.map_comp, Function.comp_def, ofList_nil] using h

end ListFinite

end Cslib.RandomOracle
